/NOPR

! Create rewrite control macro
*CREATE,rewctrl.paransys
/OUTPUT,control,paransys
*VWRITE,'PARANSYS_GO=%PARANSYS_GO%'
%C
*VWRITE,'PARANSYS_RUNS=%PARANSYS_RUNS%'
%C
*VWRITE,'PARANSYS_DONE=%PARANSYS_DONE%'
%C
*VWRITE,'PARANSYS_KILL=%PARANSYS_KILL%'
%C
/OUTPUT
*END

*CREATE,P26EXP.paransys.mac
/NOPR
_P26_PAR = ARG1
*IF,_P26_PAR,EQ,0,THEN
/OUTPUT,'_p26_out','csv','.'
*VWRITE,'!---PARANSYS---!'
%C
/OUTPUT,TERM
*ELSE
*GET,_P26_SIZE,VARI,1,NSETS
*DEL,_P26_EXPORT
*DIM,_P26_EXPORT,TABLE,_P26_SIZE,1
VGET,_P26_EXPORT(1,0),_P26_PAR
/OUTPUT,'_p26_out','csv',,APPEND
*VWRITE,'*P26VAR=%_P26_PAR%'
%C
*VWRITE,_P26_EXPORT(1,0)
%G
*VWRITE,'*P26END'
%C
/OUTPUT,TERM
*ENDIF
/GOPR
*END

PARANSYS_LOOP = 1
PARANSYS_RUNS = 0

! Main loop
*DOWHILE,PARANSYS_LOOP
    ! Import controls
    /INPUT,control.paransys

    ! Possible commands
    ! RUN
    *IF,PARANSYS_GO,GE,1,THEN
        ! Run baby, run
        
        ! First control.paransys need to be rewrited
        PARANSYS_GO = 0
        PARANSYS_RUNS = PARANSYS_RUNS
        PARANSYS_DONE = 0
        PARANSYS_KILL = 0
        /INPUT,rewctrl,paransys

        ! Delete older parameters
        /DELETE,par_out.paransys,,,BOTH

        ! Clear database
        /CLEAR,start.ans

        ! Import current parameters
        /INPUT,par_in.paransys

        ! Run main code
        /GOPR
        /INPUT,mainscript,paransys
        /NOPR

        ! Export current parameters
        PARSAV,ALL,par_out.paransys

        ! Rewrite control.paransys again
        /INPUT,control.paransys
        PARANSYS_GO = 0
        PARANSYS_RUNS = PARANSYS_RUNS+1
        PARANSYS_DONE = 1
        PARANSYS_KILL = 0
        /INPUT,rewctrl,paransys

    ! Close ANSYS exiting the loop
    *ELSEIF,PARANSYS_KILL,GE,1,THEN
        /DELETE,control.paransys,,,BOTH
        *EXIT
    *ENDIF

    ! Wait some seconds before running again 
    PARANSYS_LOOP = 1
    /WAIT,1
*ENDDO